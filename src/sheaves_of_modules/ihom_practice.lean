import algebra.category.Group.limits
import category_theory.preadditive.functor_category
import topology.sheaves.sheaf
import topology.sheaves.sheaf_condition.unique_gluing

import sheaves_of_modules.defs

noncomputable theory

open category_theory category_theory.limits

namespace Top.presheaf

section

universe u

variables {X : Top.{u}}

open topological_space Top opposite

/--
The inclusion `U ‚ü∂ X`
-/
@[simps] def emb (U : opens X) : Top.of U ‚ü∂ X :=
{ to_fun := (coe : U ‚Üí X),
  continuous_to_fun := continuous_subtype_val }

/--
If `U` is open, then any open subset `V` is also an open subset of `X`.
-/
def emb.to_global_subset {U : opens X} (V : opens (Top.of U)) : opens X :=
‚ü®subtype.val '' V.1, (is_open.open_embedding_subtype_coe U.2).is_open_map _ V.2‚ü©

/--
If `U ‚äÜ V` are open sets in `X` and `W ‚äÜ U` is open in `U` then `W` is open in `V`.
-/
def emb.of_subset {U V : opens X} (inc : U ‚ü∂ V) (W : opens (Top.of U)) : opens (Top.of V) :=
{ val := (Œª p, ‚ü®p.1, le_of_hom inc p.2‚ü© : U ‚Üí V) '' W.1,
  property := let ‚ü®O, hO1, hO2‚ü© := is_open_induced_iff.mp W.2 in
    is_open_induced_iff.mpr ‚ü®subtype.val '' W.1,
    begin
      apply_fun set.image subtype.val at hO2,
      rw ‚ÜêhO2,
      apply (is_open.open_embedding_subtype_coe U.2).is_open_map,
      apply is_open.preimage,
      continuity,
    end, begin
      ext ‚ü®x, hx‚ü©, split,
      { rintros ‚ü®p, hp1, hp2‚ü©,
        rw set.mem_image,
        refine ‚ü®p, hp1, subtype.ext_iff_val.mpr hp2‚ü©, },
      { rintros ‚ü®p, hp1, hp2‚ü©,
        rw [‚Üêhp2, set.mem_preimage, set.mem_image],
        refine ‚ü®p, hp1, rfl‚ü©, },
    end‚ü© }

/--
If `U ‚äÜ V` are open sets in `X` and `W‚ÇÅ ‚äÜ W‚ÇÇ ‚äÜ U` is open in `U` then 
`W‚ÇÅ ‚äÜ W‚ÇÇ` is open in `V`.
-/
def emb.of_subset_hom {U V : opens X} (inc : U ‚ü∂ V) 
  {W‚ÇÅ W‚ÇÇ : opens (Top.of U)} (i : W‚ÇÅ ‚ü∂ W‚ÇÇ) :
  emb.of_subset inc W‚ÇÅ ‚ü∂ emb.of_subset inc W‚ÇÇ :=
hom_of_le $ Œª _ ‚ü®q, hq1, hq2‚ü©, ‚ü®q, le_of_hom i hq1, hq2‚ü©

lemma emb.of_subset_id (U : opens X) (W : opens (Top.of U)) :
  emb.of_subset (ùüô U) W = W :=
begin
  ext x, split,
  { rintros ‚ü®p, hp, rfl‚ü©, dsimp, erw opens.mem_coe at hp, convert hp, ext, refl, },
  { intros h, rw opens.mem_coe at h, refine ‚ü®x, h, _‚ü©, ext, refl, },
end

lemma emb.of_subset_comp 
  {U V W : opens X} (iUV : U ‚ü∂ V) (iVW : V ‚ü∂ W) (W : opens (Top.of U)) :
  emb.of_subset (iUV ‚â´ iVW) W = emb.of_subset iVW (emb.of_subset iUV W) :=
begin
  ext x, split,
  { rintros ‚ü®p, hp, rfl‚ü©, exact ‚ü®‚ü®p, le_of_hom iUV p.2‚ü©, ‚ü®p, hp, rfl‚ü©, rfl‚ü©, },
  { rintros ‚ü®p, ‚ü®q, hq, rfl‚ü©, rfl‚ü©, exact ‚ü®q, hq, rfl‚ü©, },
end

lemma emb.open_embedding (U : opens X) : open_embedding (emb U) :=
is_open.open_embedding_subtype_coe U.2

/--
Let `F` is a presheaf of abelian groups on `X` and `U ‚äÜ X` is open, this is
`F |_ V`, i.e. `(F |_ V)(W) = F(W)`
-/
@[simps] def restrict' (F : presheaf AddCommGroup.{u} X) (U : opens X) : 
  presheaf AddCommGroup (Top.of U) :=
(emb.open_embedding U).is_open_map.functor.op ‚ãô F

localized "infix `|_`:40 := Top.presheaf.restrict'" in sheaf

/--
any presheaf morphism `F |_ U ‚ü∂ G |_ U` gives a group morphism `F(U) ‚ü∂ G(U)`
by taking gloabal sections.
-/
@[simps] def sections_map_from_restrict' {F G : presheaf AddCommGroup.{u} X} {U : opens X}
  (Œ± : F |_ U ‚ü∂  G |_ U) : 
  F.obj (op U) ‚ü∂ G.obj (op U) :=
F.map (hom_of_le $ by { rintros _ ‚ü®x, hx, rfl‚ü©, exact x.2 } :
  (emb.open_embedding U).is_open_map.functor.obj ‚ä§ ‚ü∂ U).op ‚â´ Œ±.app (op ‚ä§) ‚â´
  G.map (hom_of_le $ Œª x hx, ‚ü®‚ü®x, hx‚ü©, ‚ü®‚ü©, rfl‚ü© :
    U ‚ü∂ (emb.open_embedding U).is_open_map.functor.obj ‚ä§).op

/--
restriction is functorial.
-/
@[simps] def restrict_functor (U : opens X) : 
  presheaf AddCommGroup.{u} X ‚•§ presheaf AddCommGroup (Top.of U) :=
{ obj := Œª F, F |_ U,
  map := Œª F G Œ±,
  { app := Œª V, Œ±.app _,
    naturality' := Œª V W inc,
    begin
      ext x,
      erw [restrict'_map, Œ±.naturality, restrict'_map, comp_apply],
    end },
  map_id' := Œª F,
  begin
    ext U x,
    simp only [nat_trans.id_app, id_apply],
  end,
  map_comp' := Œª F G H Œ± Œ≤,
  begin
    ext U x,
    simp only [nat_trans.comp_app],
  end }

/--
Some glue: if `W ‚äÜ U ‚äÜ V` are open sets of `X`, then
`(F |_ U)(W) ‚âÖ (F |_ V)(W)`
-/
@[reducible] def restrict_subset_sections 
  (F : presheaf AddCommGroup.{u} X) {U V : opens X} (inc : U ‚ü∂ V)
  (W : opens (Top.of U)) :
  (F |_ U).obj (op W) ‚âÖ (F |_ V).obj (op $ emb.of_subset inc W) :=
{ hom := F.map (quiver.hom.op $ hom_of_le
    begin
      rintros p ‚ü®‚ü®q, hq1‚ü©, ‚ü®x, hx1, hx2‚ü©, rfl‚ü©,
      dsimp only at hx2,
      refine ‚ü®x, hx1, _‚ü©,
      rw ‚Üêhx2,
      refl,
    end : op ((emb.open_embedding U).is_open_map.functor.obj W) ‚ü∂
      op ((emb.open_embedding V).is_open_map.functor.obj (emb.of_subset inc W))),
  inv := F.map (quiver.hom.op $ hom_of_le
    begin
      rintros p ‚ü®q, hq, rfl‚ü©,
      refine ‚ü®‚ü®q.1, le_of_hom inc q.2‚ü©, ‚ü®q, hq, rfl‚ü©, rfl‚ü©,
    end : op ((emb.open_embedding V).is_open_map.functor.obj (emb.of_subset inc W)) ‚ü∂
      op ((emb.open_embedding U).is_open_map.functor.obj W)),
  hom_inv_id' := by { rw [‚ÜêF.map_comp, ‚Üêop_comp], convert F.map_id _ },
  inv_hom_id' := by { rw [‚ÜêF.map_comp, ‚Üêop_comp], convert F.map_id _ } }

/--
Auxiliary for:
If `U ‚äÜ V` are open sets, then any presheaf morphism `F |_ V ‚ü∂ G |_ V` induces
a morphism `F |_ U ‚ü∂ F |_ V`
-/
@[simps] def restrict_subset_sections_map.app {F G : presheaf AddCommGroup.{u} X}
  {U V : opens X} (inc : U ‚ü∂ V)
  (Œ± : F |_ V ‚ü∂ G |_ V) (W : opens (Top.of U)):
  (F |_ U).obj (op W) ‚ü∂ (G |_ U).obj (op W) :=
{ to_fun := Œª s, (restrict_subset_sections G inc W).inv $ Œ±.app _ $
      (restrict_subset_sections F inc W).hom s,
  map_zero' := by rw [map_zero, map_zero, map_zero],
  map_add' := Œª x y, by rw [map_add, map_add, map_add] }

/--
Auxiliary for:
If `U ‚äÜ V` are open sets, then any presheaf morphism `F |_ V ‚ü∂ G |_ V` induces
a morphism `F |_ U ‚ü∂ F |_ V`
-/
lemma restrict_subset_sections_map.naturality {F G : presheaf AddCommGroup.{u} X}
  {U V : opens X} (inc : U ‚ü∂ V)
  (Œ± : restrict' F V ‚ü∂ restrict' G V)
  (W‚ÇÅ W‚ÇÇ : (opens (Top.of U)))
  (i : W‚ÇÅ ‚ü∂ W‚ÇÇ) :
  (restrict' F U).map i.op ‚â´ restrict_subset_sections_map.app inc Œ± W‚ÇÅ =
    restrict_subset_sections_map.app inc Œ± W‚ÇÇ ‚â´ (restrict' G U).map i.op :=
begin
  ext x,
  simp only [restrict'_map, quiver.hom.unop_op, restrict_subset_sections_map.app, comp_apply,
    add_monoid_hom.coe_mk],
  simp only [‚Üêcomp_apply],
  simp only [‚Üêcomp_apply, ‚ÜêF.map_comp, ‚Üêop_comp],
  generalize_proofs h1 h2 h3 h4 h5 h6,
  rw [show hom_of_le h3 ‚â´ h1.functor.map i = h2.functor.map (emb.of_subset_hom inc i) ‚â´
    hom_of_le h5, from rfl, op_comp, F.map_comp, category.assoc _ _ (Œ±.app _)],
  have := Œ±.naturality (emb.of_subset_hom inc i).op,
  dsimp at this,
  erw this,
  simp only [category.assoc],
  congr' 3,
  rw [‚ÜêG.map_comp, ‚ÜêG.map_comp, ‚Üêop_comp, ‚Üêop_comp],
  congr' 1,
end

/--
If `U ‚äÜ V` are open sets, then any presheaf morphism `F |_ V ‚ü∂ G |_ V` induces
a morphism `F |_ U ‚ü∂ F |_ V`
-/
@[simps] def restrict_subset_sections_map {F G : presheaf AddCommGroup.{u} X}
  {U V : opens X} (inc : U ‚ü∂ V)
  (Œ± : F |_ V ‚ü∂ G |_ V) :
  F |_ U ‚ü∂ G |_ U :=
{ app := Œª W, restrict_subset_sections_map.app inc Œ± W.unop,
  naturality' := Œª W‚ÇÅ W‚ÇÇ i, restrict_subset_sections_map.naturality inc Œ± _ _ i.unop }

/--
The morphsisms between restricted presheaves form an abelian group.
-/
instance (F G : presheaf AddCommGroup.{u} X) (U : opens X) :
  add_comm_group (F |_ U ‚ü∂ G |_ U) :=
begin
  haveI i1 : preadditive (presheaf AddCommGroup (Top.of U)) :=
    category_theory.functor_category_preadditive,
  exactI i1.1 (restrict' F U) (restrict' G U),
end

@[simp] lemma sections_map_from_restrict'_zero {F G : presheaf AddCommGroup.{u} X} {U : opens X} :
  sections_map_from_restrict' (0 : F |_ U ‚ü∂ G |_ U) = 0 :=
by ext; simp

lemma sections_map_from_restrict'_add {F G : presheaf AddCommGroup.{u} X} {U : opens X}
  (Œ± Œ≤ : F |_ U ‚ü∂ G |_ U) :
  sections_map_from_restrict' (Œ± + Œ≤) = 
  sections_map_from_restrict' Œ± + 
  sections_map_from_restrict' Œ≤ :=
by ext; simp

/--
The group homomorphism from `Hom_{Psh}(F |_ U, G |_ U)` to `Hom_{Ab}(F U, G U)`
-/
@[simps] def sections_map_from_restrict'_add_monoid_hom (F G : presheaf AddCommGroup.{u} X) (U : opens X) :
  AddCommGroup.of (F |_ U ‚ü∂ G |_ U) ‚ü∂ 
  AddCommGroup.of (F.obj (op U) ‚ü∂ G.obj (op U)) :=
{ to_fun := sections_map_from_restrict',
  map_zero' := sections_map_from_restrict'_zero,
  map_add' := sections_map_from_restrict'_add }

lemma restrict_subset_sections_map_zero {F G : presheaf AddCommGroup.{u} X}
  {U V : opens X} (inc : U ‚ü∂ V) :
  restrict_subset_sections_map inc (0 : restrict' F V ‚ü∂ restrict' G V) = 0 :=
by { ext, simp }

lemma restrict_subset_sections_map_add {F G : presheaf AddCommGroup.{u} X}
  {U V : opens X} (inc : U ‚ü∂ V) (Œ± Œ≤ : restrict' F V ‚ü∂ restrict' G V) :
  restrict_subset_sections_map inc (Œ± + Œ≤) = restrict_subset_sections_map inc Œ± +
  restrict_subset_sections_map inc Œ≤ :=
by { ext, simp }

lemma restrict_subset_sections_map_id {F G : presheaf AddCommGroup.{u} X} (U : opens X)
  (Œ± : F |_ U ‚ü∂ G |_ U) : 
  restrict_subset_sections_map (ùüô U) Œ± = Œ± :=
begin
  ext W x,
  simp only [restrict_subset_sections_map_app, restrict_subset_sections_map.app_apply],
  erw [‚Üêcomp_apply, ‚Üêcomp_apply, ‚ÜêŒ±.naturality],
  swap,
  { refine eq_to_hom _,
    rw emb.of_subset_id U W.unop,
    refl, },
  dsimp,
  rw [‚Üêcategory.assoc, ‚ÜêF.map_comp, ‚Üêop_comp],
  congr' 1,
  convert category.id_comp _,
  convert F.map_id _,
end

lemma restrict_subset_sections_map_comp {F G : presheaf AddCommGroup.{u} X} {U V W : opens X}
  (iUV : U ‚ü∂ V) (iVW : V ‚ü∂ W) 
  (Œ± : F |_ W ‚ü∂ G |_ W) :
  restrict_subset_sections_map (iUV ‚â´ iVW) Œ± =
  restrict_subset_sections_map iUV (restrict_subset_sections_map iVW Œ±) :=
begin
  ext O x,
  simp only [restrict_subset_sections_map_app, restrict_subset_sections_map.app_apply],
  simp only [‚Üêcomp_apply, category.assoc, ‚ÜêG.map_comp, ‚Üêop_comp],
  rw [‚Üêcategory.assoc _ _ (Œ±.app _ ‚â´ _), ‚ÜêF.map_comp, ‚Üêop_comp],
  congr' 1,
  change _ = _ ‚â´ Œ±.app (op (emb.of_subset iVW (emb.of_subset iUV _))) ‚â´ _,
  generalize_proofs h1 h2 h3 h4 h5 h6 h7 h8 h9,
  rw [show Œ±.app (op (emb.of_subset iVW (emb.of_subset iUV O.unop))) =
    F.map ((emb.open_embedding W).is_open_map.functor.op.map (eq_to_hom _)) ‚â´
      Œ±.app (op (emb.of_subset (iUV ‚â´ iVW) O.unop)) ‚â´
      G.map ((emb.open_embedding W).is_open_map.functor.op.map (eq_to_hom _)),
    from _, category.assoc, category.assoc, ‚ÜêG.map_comp, ‚Üêcategory.assoc (F.map _) (F.map _),
    ‚ÜêF.map_comp],
  congr' 1,
  { rw emb.of_subset_comp, },
  { rw emb.of_subset_comp, },
  { erw [‚Üêcategory.assoc, Œ±.naturality, category.assoc, ‚ÜêG.map_comp],
    symmetry,
    convert category.comp_id _,
    convert G.map_id _, },
end

namespace monoidal

/--
The internal hom between `F, G` is defined to be:

* obj level: `U ‚Ü¶ Hom_{Psh}(F |_ U, G |_ U)`
* map level: if `U ‚äÜ V` and `Œ± : F |_ V ‚ü∂ F |_U`, use `restrict_subset_sections_map`.
-/
@[simps] def ihom_obj (F G : presheaf AddCommGroup.{u} X) : presheaf AddCommGroup.{u} X :=
{ obj := Œª U, AddCommGroup.of (F |_ U.unop ‚ü∂ G |_ U.unop),
  map := Œª U V inc,
  { to_fun := Œª Œ±, restrict_subset_sections_map inc.unop Œ±,
    map_zero' := restrict_subset_sections_map_zero inc.unop,
    map_add' := Œª Œ± Œ≤, restrict_subset_sections_map_add inc.unop Œ± Œ≤ },
  map_id' := Œª U,
  begin
    ext1,
    rw [add_monoid_hom.coe_mk, unop_id, restrict_subset_sections_map_id, id_apply],
  end,
  map_comp' := Œª U V W iUV iVW,
  begin
    ext1 Œ±,
    rw [add_monoid_hom.coe_mk, comp_apply, add_monoid_hom.coe_mk, add_monoid_hom.coe_mk],
    convert restrict_subset_sections_map_comp iVW.unop iUV.unop Œ±,
  end }
-- ùìóùì∏ùì∂
-- ‚Ñã‚Ñ¥ùìÇ
localized "notation `‚ü¶` F, G`‚üß` := Top.presheaf.monoidal.ihom_obj F G" in sheaf

/--
If `F, G‚ÇÅ, G‚ÇÇ` are presheaves and `Œ≥ : G‚ÇÅ ‚ü∂ G‚ÇÇ` and `f : F |_ U ‚ü∂ G‚ÇÅ |_ U`:
`Œ≥` gives a map `G‚ÇÅ |_ U ‚ü∂ G‚ÇÇ |_ U`, composing it with `f` to get `F |_ U ‚ü∂ G‚ÇÇ |_ U`
-/
@[simps] def ihom_map' (F G‚ÇÅ G‚ÇÇ : presheaf AddCommGroup.{u} X) (Œ≥ : G‚ÇÅ ‚ü∂ G‚ÇÇ)
  (U : opens X) (f : F |_ U ‚ü∂ G‚ÇÅ |_ U) :
  F |_ U ‚ü∂ G‚ÇÇ |_ U :=
f ‚â´ (restrict_functor U).map Œ≥

lemma ihom_map'_zero (F G‚ÇÅ G‚ÇÇ : presheaf AddCommGroup.{u} X) (Œ≥ : G‚ÇÅ ‚ü∂ G‚ÇÇ) (U : opens X) :
  ihom_map' F G‚ÇÅ G‚ÇÇ Œ≥ U 0 = 0 :=
by ext; simp

lemma ihom_map'_add (F G‚ÇÅ G‚ÇÇ : presheaf AddCommGroup.{u} X) (Œ≥ : G‚ÇÅ ‚ü∂ G‚ÇÇ) (U : opens X)
  (Œ± Œ≤ : restrict' F U ‚ü∂ restrict' G‚ÇÅ U) :
  ihom_map' F G‚ÇÅ G‚ÇÇ Œ≥ U (Œ± + Œ≤) = ihom_map' F G‚ÇÅ G‚ÇÇ Œ≥ U Œ± + ihom_map' F _ _ Œ≥ U Œ≤ :=
by ext; simp

lemma ihom_map'_naturality (F G‚ÇÅ G‚ÇÇ : presheaf AddCommGroup.{u} X)
  (Œ≥ : G‚ÇÅ ‚ü∂ G‚ÇÇ) (U : opens X) (Œ± : F |_ U ‚ü∂ G‚ÇÅ |_ U)
  {W‚ÇÅ W‚ÇÇ : opens (Top.of U)} (inc : W‚ÇÅ ‚ü∂ W‚ÇÇ) :
  (F |_ U).map inc.op ‚â´ (ihom_map' F G‚ÇÅ G‚ÇÇ Œ≥ U Œ±).app (op W‚ÇÅ) =
  (ihom_map' F G‚ÇÅ G‚ÇÇ Œ≥ U Œ±).app (op W‚ÇÇ) ‚â´ (G‚ÇÇ |_ U).map inc.op :=
begin
  ext x,
  simp only [restrict'_map, quiver.hom.unop_op, comp_apply, ihom_map'_app_apply],
  simp only [‚Üêcomp_apply, category.assoc, ‚ÜêG‚ÇÇ.map_comp],
  erw [‚ÜêŒ≥.naturality,
    ‚Üêcategory.assoc (Œ±.app _), ‚ÜêŒ±.naturality],
  congr,
  exact inc.op,
end

/--
internal presheaf hom is functorial
-/
@[simps] def ihom_map_app (F G‚ÇÅ G‚ÇÇ : presheaf AddCommGroup.{u} X) (Œ≥ : G‚ÇÅ ‚ü∂ G‚ÇÇ) (U : opens X) :
  ‚ü¶F, G‚ÇÅ‚üß.obj (op U) ‚ü∂ ‚ü¶F, G‚ÇÇ‚üß.obj (op U) :=
{ to_fun := Œª Œ±,
  { app := Œª W, (ihom_map' F G‚ÇÅ G‚ÇÇ Œ≥ U Œ±).app W,
    naturality' := Œª W‚ÇÅ W‚ÇÇ inc,
    by convert ihom_map'_naturality F G‚ÇÅ G‚ÇÇ Œ≥ U Œ± inc.unop },
  map_zero' :=
  begin
    ext W x,
    simp_rw ihom_map'_zero F G‚ÇÅ G‚ÇÇ Œ≥ U,
  end,
  map_add' := Œª _ _,
  begin
    ext W x,
    simp_rw ihom_map'_add F G‚ÇÅ G‚ÇÇ Œ≥ U,
    rw [nat_trans.app_add, nat_trans.app_add],
  end }

/--
internal presheaf hom is functorial
-/
lemma ihom_map_naturality (F G‚ÇÅ G‚ÇÇ : presheaf AddCommGroup.{u} X) (Œ≥ : G‚ÇÅ ‚ü∂ G‚ÇÇ)
  {U V : opens X} (iUV : U ‚ü∂ V) :
  ‚ü¶F, G‚ÇÅ‚üß.map iUV.op ‚â´ ihom_map_app F G‚ÇÅ G‚ÇÇ Œ≥ U =
  ihom_map_app F G‚ÇÅ G‚ÇÇ Œ≥ V ‚â´ (ihom_obj F G‚ÇÇ).map iUV.op :=
begin
  ext f W x,
  simp only [comp_apply, ihom_obj_map_apply, quiver.hom.unop_op, ihom_map_app_apply_app,
    ihom_map'_app_apply, restrict_subset_sections_map_app, restrict_subset_sections_map.app_apply],
  simp only [‚Üêcomp_apply, category.assoc],
  rw [‚ÜêŒ≥.naturality],
  congr' 1,
end

/--
internal presheaf hom is functorial
-/
@[simps] def ihom_map (F G‚ÇÅ G‚ÇÇ : presheaf AddCommGroup.{u} X) (Œ≥ : G‚ÇÅ ‚ü∂ G‚ÇÇ) :
  ‚ü¶F, G‚ÇÅ‚üß ‚ü∂ ‚ü¶F, G‚ÇÇ‚üß :=
{ app := Œª U, ihom_map_app F G‚ÇÅ G‚ÇÇ Œ≥ U.unop,
  naturality' := Œª U V iUV, by convert ihom_map_naturality F G‚ÇÅ G‚ÇÇ Œ≥ iUV.unop }

lemma ihom_map_id (F G : presheaf AddCommGroup.{u} X) :
  ihom_map F G G (ùüô G) = ùüô _ :=
begin
  ext f U W x,
  simp only [ihom_map_app_2, ihom_map_app_apply_app, ihom_map'_app_apply, nat_trans.id_app,
    id_apply],
end

lemma ihom_map_comp (F G‚ÇÅ G‚ÇÇ G‚ÇÉ : presheaf AddCommGroup.{u} X) (g‚ÇÅ‚ÇÇ : G‚ÇÅ ‚ü∂ G‚ÇÇ) (g‚ÇÇ‚ÇÉ : G‚ÇÇ ‚ü∂ G‚ÇÉ) :
  ihom_map F _ _ (g‚ÇÅ‚ÇÇ ‚â´ g‚ÇÇ‚ÇÉ) = ihom_map F _ _ g‚ÇÅ‚ÇÇ ‚â´ ihom_map F _ _ g‚ÇÇ‚ÇÉ :=
begin
  ext f U W x,
  simp only [ihom_map_app_2, ihom_map_app_apply_app, ihom_map'_app_apply, nat_trans.comp_app,
    comp_apply],
end

/--
internal presheaf hom with source `F` is functorial

* obj level : `G ‚Ü¶ ihom F G`
* map level : `Œ± : G‚ÇÅ ‚ü∂ G‚ÇÇ`, a map `ihom F G‚ÇÅ ‚ü∂ ihom F G‚ÇÇ` is obtained by
  `U ‚Ü¶ (f ‚Ü¶ f ‚â´ Œ± |_ U) `
-/
@[simps] def ihom (F : presheaf AddCommGroup.{u} X) :
  presheaf AddCommGroup.{u} X ‚•§ presheaf AddCommGroup.{u} X :=
{ obj := ihom_obj F,
  map := ihom_map F,
  map_id' := ihom_map_id F,
  map_comp' := Œª _ _ _, ihom_map_comp F _ _ _ }

end monoidal

end

end Top.presheaf

namespace Top.sheaf

open Top Top.presheaf topological_space
open category_theory category_theory.limits

universe u

variables {X : Top.{u}}

alias presheaf.monoidal.ihom_obj ‚Üê presheaf.ihom_obj

open_locale sheaf

lemma restrict_is_sheaf {F : Top.presheaf AddCommGroup.{u} X} 
  (hF : is_sheaf F) (U : opens X) :
  is_sheaf (F |_ U) :=
is_sheaf_of_open_embedding (Top.presheaf.emb.open_embedding U) hF

def sheaf_restrict (F : sheaf AddCommGroup.{u} X) (U : opens X) :
  sheaf AddCommGroup.{u} (Top.of U) := 
‚ü®_, restrict_is_sheaf F.cond U‚ü©

localized "infix (name := sheaf_restrict) `|_`:40 := Top.sheaf.sheaf_restrict" in sheaf

namespace monoidal

open opposite

lemma ihom_obj_is_sheaf_of_is_sheaf {F G : Top.presheaf AddCommGroup.{u} X}
  (hF : is_sheaf F) (hG : is_sheaf G) : is_sheaf ‚ü¶F, G‚üß :=
sorry

open category_theory.grothendieck_topology

@[simps] def ihom_obj (F G : sheaf AddCommGroup.{u} X) : sheaf AddCommGroup.{u} X :=
{ val := ‚ü¶F.val, G.val‚üß,
  cond := ihom_obj_is_sheaf_of_is_sheaf F.cond G.cond }

localized "notation (name := sheaf_ihom_obj) `‚ü¶`F, G`‚üß` := 
  Top.sheaf.monoidal.ihom_obj F G" in sheaf

@[simps] def ihom (F : sheaf AddCommGroup.{u} X) :
  sheaf AddCommGroup.{u} X ‚•§ sheaf AddCommGroup.{u} X :=
{ obj := Œª G, ‚ü¶F, G‚üß,
  map := Œª G‚ÇÅ G‚ÇÇ Œ±, ‚ü®presheaf.monoidal.ihom_map _ _ _ Œ±.val‚ü©,
  map_id' := Œª G,
  begin
    ext U x y z,
    simp only [Sheaf.category_theory.category_id_val, presheaf.monoidal.ihom_map_app_2,
      presheaf.monoidal.ihom_map_app_apply_app, presheaf.monoidal.ihom_map'_app_apply,
      nat_trans.id_app, id_apply],
  end,
  map_comp' := Œª G‚ÇÅ G‚ÇÇ G‚ÇÉ Œ± Œ≤,
  begin
    ext U x y z,
    simp only [Sheaf.category_theory.category_comp_val, presheaf.monoidal.ihom_map_app_2,
      presheaf.monoidal.ihom_map_app_apply_app, presheaf.monoidal.ihom_map'_app_apply,
      nat_trans.comp_app, comp_apply],
  end }

end monoidal

end Top.sheaf

namespace SHEAF_OF_MODULES

open topological_space opposite

variables {X : RINGED_SPACE} (M1 M2 : SHEAF_OF_MODULES X)

namespace monoidal

open Top.sheaf.monoidal

local attribute [instance] SHEAF_OF_MODULES.module_structure

@[simps] def smul_ihom_obj (U : (opens (‚ÜëX : TOP))·µí·µñ) (r : X.presheaf.obj U)
  (s : (ihom_obj M1.ab_sheaf M2.ab_sheaf).1.obj U) : 
  (ihom_obj M1.ab_sheaf M2.ab_sheaf).1.obj U :=
{ app := Œª V, 
      show M1.ab_sheaf.val.obj 
            (op ((Top.presheaf.emb.open_embedding U.unop).is_open_map.functor.obj V.unop)) ‚ü∂ 
          M2.ab_sheaf.val.obj 
            (op ((Top.presheaf.emb.open_embedding U.unop).is_open_map.functor.obj V.unop)), 
      from let r' : X.presheaf.obj 
        (op ((Top.presheaf.emb.open_embedding U.unop).is_open_map.functor.obj V.unop)) := 
        X.presheaf.map (hom_of_le
          begin 
            rintros _ ‚ü®x, -, rfl‚ü©, exact x.2,
          end : (Top.presheaf.emb.open_embedding U.unop).is_open_map.functor.obj V.unop ‚ü∂ U.unop).op r in
      r' ‚Ä¢ s.app V,
    naturality' := Œª V V' i, 
    begin 
      dsimp,
      ext : 1,
      simp only [comp_apply, add_monoid_hom.smul_apply],
      erw [‚Üêcomp_apply, s.naturality],
      erw M2.compatibility_bit,
      congr,
      rw [‚Üêcomp_apply, ‚ÜêX.presheaf.map_comp],
      refl,
    end }

instance has_smul_ihom_obj (U : (opens (‚ÜëX : TOP))·µí·µñ) : 
  has_smul (X.presheaf.obj U) $ (ihom_obj M1.ab_sheaf M2.ab_sheaf).1.obj U :=
{ smul := smul_ihom_obj M1 M2 U }

lemma smul_ihom_obj.one_smul (U : (opens (‚ÜëX : TOP))·µí·µñ) 
  (s : (ihom_obj M1.ab_sheaf M2.ab_sheaf).1.obj U) :
  smul_ihom_obj M1 M2 U 1 s = s :=
by ext; simp

lemma smul_ihom_obj.mul_smul (U : (opens (‚ÜëX : TOP))·µí·µñ)
    (r r' : (X.presheaf.obj U))
  (s : (ihom_obj M1.ab_sheaf M2.ab_sheaf).1.obj U) :
  smul_ihom_obj _ _ _ (r * r') s = 
  smul_ihom_obj _ _ _ r (smul_ihom_obj _ _ _ r' s) :=
by ext V t; simp [mul_smul]

instance mul_action_ihom_obj (U : (opens (‚ÜëX : TOP))·µí·µñ) : 
  mul_action (X.presheaf.obj U) $ (ihom_obj M1.ab_sheaf M2.ab_sheaf).1.obj U :=
{ one_smul := smul_ihom_obj.one_smul _ _ _,
  mul_smul := smul_ihom_obj.mul_smul _ _ _,
  ..SHEAF_OF_MODULES.monoidal.has_smul_ihom_obj M1 M2 U}

lemma smul_ihom_obj.smul_zero (U : (opens (‚ÜëX : TOP))·µí·µñ) 
  (r : X.presheaf.obj U) :
  smul_ihom_obj M1 M2 U r 0 = 0 :=
by ext; simp

lemma smul_ihom_obj.smul_add (U : (opens (‚ÜëX : TOP))·µí·µñ) 
  (r : X.presheaf.obj U)
  (x y : (ihom_obj M1.ab_sheaf M2.ab_sheaf).1.obj U) :
  smul_ihom_obj M1 M2 U r (x + y) = 
  smul_ihom_obj M1 M2 U r x + 
  smul_ihom_obj M1 M2 U r y :=
by ext; simp [smul_add]

instance distrib_mul_action_ihom_obj (U : (opens (‚ÜëX : TOP))·µí·µñ) : 
  distrib_mul_action (X.presheaf.obj U) $ (ihom_obj M1.ab_sheaf M2.ab_sheaf).1.obj U :=
{ smul_zero := smul_ihom_obj.smul_zero _ _ _,
  smul_add := smul_ihom_obj.smul_add _ _ _,
  ..SHEAF_OF_MODULES.monoidal.mul_action_ihom_obj M1 M2 U}

lemma smul_ihom_obj.add_smul (U : (opens (‚ÜëX : TOP))·µí·µñ) 
  (r r' : X.presheaf.obj U)
  (x : (ihom_obj M1.ab_sheaf M2.ab_sheaf).1.obj U) :
  smul_ihom_obj M1 M2 U (r + r') x = 
  smul_ihom_obj M1 M2 U r x + 
  smul_ihom_obj M1 M2 U r' x :=
by ext; simp [add_smul]

lemma smul_ihom_obj.zero_smul (U : (opens (‚ÜëX : TOP))·µí·µñ) 
  (x : (ihom_obj M1.ab_sheaf M2.ab_sheaf).1.obj U) :
  smul_ihom_obj M1 M2 U 0 x = 0 :=
by ext; simp


instance module_ihom_obj (U : (opens (‚ÜëX : TOP))·µí·µñ) : 
  module (X.presheaf.obj U) $ (ihom_obj M1.ab_sheaf M2.ab_sheaf).1.obj U :=
{ add_smul := smul_ihom_obj.add_smul _ _ _,
  zero_smul := smul_ihom_obj.zero_smul _ _ _,
  ..SHEAF_OF_MODULES.monoidal.distrib_mul_action_ihom_obj M1 M2 U}

lemma smul_ihom_obj_def (U : (opens (‚ÜëX : TOP))·µí·µñ) 
  (r : X.presheaf.obj U)
  (x : (ihom_obj M1.ab_sheaf M2.ab_sheaf).1.obj U) :
  r ‚Ä¢ x = smul_ihom_obj _ _ _ r x := rfl

def ihom_obj : SHEAF_OF_MODULES X :=
{ ab_sheaf := ihom_obj M1.ab_sheaf M2.ab_sheaf,
  module_structure := SHEAF_OF_MODULES.monoidal.module_ihom_obj _ _,
  compatibility_bit := Œª U V i r x, 
  begin 
    delta ihom_obj,
    ext W s, 
    dsimp only [smul_ihom_obj_def, smul_ihom_obj_app],
    rw [add_monoid_hom.smul_apply, Top.presheaf.monoidal.ihom_obj_map_apply,
      Top.presheaf.restrict_subset_sections_map_app,
      Top.presheaf.restrict_subset_sections_map.app_apply,
      smul_ihom_obj_app],
    dsimp,
    conv_rhs { rw [‚Üêcomp_apply, ‚ÜêX.presheaf.map_comp], },
    erw M2.compatibility_bit,
    rw [‚Üêcomp_apply, ‚ÜêX.presheaf.map_comp],
    refl,
  end }

@[simps] def ihom_map (F G‚ÇÅ G‚ÇÇ : SHEAF_OF_MODULES X) (Œ± : G‚ÇÅ ‚ü∂ G‚ÇÇ) :
  ihom_obj F G‚ÇÅ ‚ü∂ ihom_obj F G‚ÇÇ :=
{ ab_sheaf := (Top.sheaf.monoidal.ihom F.ab_sheaf).map Œ±.ab_sheaf,
  map_smul := Œª U r x, 
  begin 
    ext V s,
    dsimp [Top.presheaf.monoidal.ihom_map', smul_ihom_obj_def],
    rw [comp_apply, add_monoid_hom.smul_apply, comp_apply, ‚ÜêŒ±.map_smul],
  end }

@[simps] def ihom (F : SHEAF_OF_MODULES X) : 
  SHEAF_OF_MODULES X ‚•§ SHEAF_OF_MODULES X :=
{ obj := ihom_obj F,
  map := ihom_map F,
  map_id' := Œª G, 
  begin 
    ext1,
    exact (Top.sheaf.monoidal.ihom F.ab_sheaf).map_id _,
  end,
  map_comp' := Œª G‚ÇÄ G‚ÇÅ G‚ÇÇ Œ± Œ≤, 
  begin 
    ext1,
     exact (Top.sheaf.monoidal.ihom F.ab_sheaf).map_comp _ _,
  end }

end monoidal

end SHEAF_OF_MODULES
